import org.apache.tools.ant.filters.FixCrLfFilter

plugins {
    id 'base'
    id 'application'
    id 'java'
    id 'distribution'
}

repositories {
    mavenCentral()
}

dependencies {
    // JInput core API
    implementation 'net.java.jinput:jinput:2.0.10'
    
    // Native libraries for all platforms
    implementation 'net.java.jinput:jinput:2.0.10:natives-all'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

base {
    archivesName.set('mochadoom')
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
    }
}

ext {
    javaMainClass = 'mochadoom.Engine'
}

java {
    withSourcesJar()
}

jar {
    manifest {
        attributes('Main-Class': javaMainClass)
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    from('wads') {
        include 'doom1.wad'
    }
    from('natives') {
        include '**/*.so'
        include '**/*.dll'
        include '**/*.dylib'
        into('natives')
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    
    // Ensure native libraries are properly marked for extraction
    doFirst {
        manifest {
            attributes(
                'Implementation-Title': 'Mocha Doom',
                'Implementation-Version': version,
                'Implementation-Vendor': 'Mocha Doom Project'
            )
        }
    }
}

application {
    mainClass.set(javaMainClass)
}

distributions {
    main {
        distributionBaseName = 'mochadoom'
        contents {
            from jar
            from sourcesJar
            from 'wads'
            from('scripts') {
                include 'mochadoom*.sh'
                filter(FixCrLfFilter, eol: FixCrLfFilter.CrLf.newInstance('unix'))
            }
            from('scripts') {
                include 'mochadoom*.bat'
                filter(FixCrLfFilter, eol: FixCrLfFilter.CrLf.newInstance('dos'))
            }
            from("$projectDir") {
                include 'LICENSE.TXT'
                include 'README.md'
                include 'CONFIG.md'
                include 'PROGRAMMING.md'
                include '.doomrc'
                include 'default.cfg'
                include 'classic.cfg'
                include 'modern.cfg'
                include 'mochadoom.cfg'
            }
            into '/'
        }
    }
}

startScripts.enabled = false
distTar.enabled = false
distZip {
    eachFile { file ->
        if (file.path.matches('^(bin|lib)/.*$')) {
            file.exclude()
        }
    }
}

tasks.withType(JavaCompile).all {
    options.compilerArgs.add('-Xlint:unchecked')
    options.compilerArgs.add('-Xlint:deprecation')
}
